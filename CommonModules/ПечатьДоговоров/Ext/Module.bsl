////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для печати договоров.
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс
Функция ЦветФонаЗаменяемогоПараметра() Экспорт

	// Обращение к цвету фона через ЦветаСтиля.ФонЗаменяемогоПараметра 
	// возвращает цвет, которые не может быть сохранен в HTML (его составляющие RGB = -1).
	// Для возможности сохранения в HTML получаем абсолютный цвет.
	МетаданныеЭлементаСтиля = Метаданные.ЭлементыСтиля.ФонЗаменяемогоПараметра;
	
	Возврат Новый Цвет(МетаданныеЭлементаСтиля.Значение.Красный, 
						МетаданныеЭлементаСтиля.Значение.Зеленый,
						МетаданныеЭлементаСтиля.Значение.Синий);

КонецФункции
					
Функция СведенияОбОбъекте(СсылкаНаОбъект) Экспорт
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Договора") Тогда		 
		 СведенияОбОбъекте = Справочники.Договора.ПодготовитьПараметрыПечатиТекстаДоговора(СсылкаНаОбъект); 
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.Письмо") Тогда
		 СведенияОбОбъекте = Документы.Письмо.ПодготовитьПараметрыПечатиТекстаписьма(СсылкаНаОбъект);
	КонецЕсли;	
	
	Результат = Новый Структура();
	Результат.Вставить("СведенияОбОбъекте",СведенияОбОбъекте);
	//Результат.Вставить("ТипОбъекта",ТипЗнч(СсылкаНаОбъект));
	Возврат Результат;
КонецФункции	
// Вставляет в форматированный документ текст значения для составного параметра таким образом,
// чтобы оставить выделение только для частей параметра, а поясняющий текст возле них - без выделения.
//
// Параметры:
// 	СтрокаЗначения - Строка - текст значения для вставки. В тексте с помощью % выделены места, 
//							  в которые будут подставляться параметры.
//							  Например, "в лице %1% %2%, действующего(-ей) на основании %3".
//	ЗначенияСоставногоПараметра - Массив - значения отдельных частей параметра в том порядке, как они указаны в СтрокаЗначения.
//	ФорматированныйДокумент - ФорматированныйДокумент - документ, в котором производится замена.
//	ЭлементДокументаСПараметром - ТекстФорматированногоДокумента - элемент форматированного документа,
//								содержащий параметр, который сейчас заменяется на значение.
// 
Функция ВставитьСоставноеЗначение(СтрокаЗначения, ЗначенияСоставногоПараметра, ФорматированныйДокумент, ЭлементДокументаСПараметром) Экспорт 
	ДлинаДобавленногоТекста = 0;

	МассивПодстрок 		= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаЗначения, "%");
	ЗакладкаКонца 		= ЭлементДокументаСПараметром.ЗакладкаКонца;
	ЦветПоУмолчанию 	= ПечатьДоговоровКлиентСервер.ЦветФонаПоУмолчанию();

	Для Каждого Подстрока Из МассивПодстрок Цикл
	
		ЗаменяемыйТекст = Подстрока;
		СнятьВыделение 	= Истина;
		
		Если ЗначениеЗаполнено(ЗаменяемыйТекст) 
			И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛП(ЗаменяемыйТекст)) Тогда
			НомерПараметра = Число(СокрЛП(ЗаменяемыйТекст));
			Если НомерПараметра <= ЗначенияСоставногоПараметра.Количество() Тогда
				ЗаменяемыйТекст = ЗначенияСоставногоПараметра[НомерПараметра - 1];
				СнятьВыделение = Лев(ЗаменяемыйТекст, 1) <> "_";
			Иначе
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В строке ""%1"" номер параметра %2 превышает количество элементов в массиве ЗначенияСоставногоПараметра'"),
					СтрокаЗначения,
					НомерПараметра);
			КонецЕсли;
		КонецЕсли;
		
		НовыйЭлемент = ФорматированныйДокумент.Вставить(ЗакладкаКонца, ЗаменяемыйТекст, Тип("ТекстФорматированногоДокумента"));
		Если НовыйЭлемент <> Неопределено Тогда // В случае пустых строк элемент документ может не создаваться.
			ДлинаДобавленногоТекста = ДлинаДобавленногоТекста + СтрДлина(ЗаменяемыйТекст);

			// Оставляем исходный шрифт элемента
			НовыйЭлемент.Шрифт = ЭлементДокументаСПараметром.Шрифт;

			Если СнятьВыделение Тогда
				НовыйЭлемент.ЦветФона 	= ЦветПоУмолчанию;
			Иначе
				НовыйЭлемент.ЦветФона 	= ЭлементДокументаСПараметром.ЦветФона;
			КонецЕсли;
			
			// Запомним место вставки следующего элемента
			ЗакладкаКонца = НовыйЭлемент.ЗакладкаКонца;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДлинаДобавленногоТекста;

КонецФункции

Функция ЗначениеПараметра(Знач ИмяПараметра, СведенияОбОбъекте, ЗначенияСоставногоПараметра, Успешно) Экспорт
	Результат = Неопределено;
	Если  СведенияОбОбъекте.ТипОбъекта =  Тип("ДокументСсылка.Письмо") Тогда
		 Результат = Документы.Письмо.ЗначениеПараметра(ИмяПараметра, СведенияОбОбъекте, ЗначенияСоставногоПараметра, Успешно);
	ИначеЕсли СведенияОбОбъекте.ТипОбъекта =  Тип("СправочникСсылка.Договора") Тогда	
		 Результат = Справочники.Договора.ЗначениеПараметра(ИмяПараметра, СведенияОбОбъекте, ЗначенияСоставногоПараметра, Успешно);
	КонецЕсли;	
	Возврат Результат;
КонецФункции	

Процедура ЗаполнитьТекстОбъектаПоШаблону(ШаблонОбъекта, СсылкаНаОбъект) Экспорт
	ФорматированныйДокумент = Новый ФорматированныйДокумент;
	СведенияОбОбъекте = СведенияОбОбъекте(СсылкаНаОбъект);
	Если СведенияОбОбъекте = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Договора") Тогда
		 ДанныеШаблона = Справочники.ШаблоныДоговоров.ДанныеШаблонаДоговора(ШаблонОбъекта);
	КонецЕсли;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.Письмо") Тогда
         ДанныеШаблона = Справочники.ШаблоныПисем.ДанныеШаблонаПисьма(ШаблонОбъекта);
	 КонецЕсли;
	 
	 ТекстHTML = ДанныеШаблона.ТекстHTML;
	
	Если НЕ ЗначениеЗаполнено(ТекстHTML) Тогда
		Возврат;
	КонецЕсли;

	ФорматированныйДокумент.УстановитьHTML(ТекстHTML, ДанныеШаблона.Вложения);
	
	// Заменяем параметры по тексту на значения из базы.
	ЦветФонаЗаменяемогоПараметра = ЦветФонаЗаменяемогоПараметра();
	ЦветПоУмолчанию = ПечатьДоговоровКлиентСервер.ЦветФонаПоУмолчанию();
	
	ЭлементыКОбнулению = Новый Массив;
	
	ЗакладкаКонцаДокумента = ФорматированныйДокумент.ПолучитьЗакладкуКонца();
	ЗначенияСоставногоПараметра = Новый Массив;
	ОбластьНачалоПараметра = ФорматированныйДокумент.НайтиТекст("{");
	Пока ОбластьНачалоПараметра <> Неопределено Цикл
		 ОбластьКонецПараметра = ФорматированныйДокумент.НайтиТекст("}", ОбластьНачалоПараметра.ЗакладкаНачала);
		Если ОбластьКонецПараметра = Неопределено Тогда
			// Для параметра не задана закрывающая скобка и он продолжается до конца текста, поэтому выходим из цикла.
			Прервать;
		КонецЕсли;
		
		МассивЭлементов = ФорматированныйДокумент.СформироватьЭлементы(
			ОбластьНачалоПараметра.ЗакладкаНачала, ОбластьКонецПараметра.ЗакладкаКонца);

		ЭлементыКОбнулению.Очистить();	
		ПоследнийТекстовыйЭлемент = Неопределено;
			
		Сч = 0;
		ДлинаДобавленногоТекста = 0;
		Пока Сч < МассивЭлементов.Количество() Цикл
	         ЭлементДокумента = МассивЭлементов[Сч];
		
			Если ТипЗнч(ЭлементДокумента) <> Тип("ТекстФорматированногоДокумента") Тогда
				Сч = Сч + 1;
				Продолжить;
			КонецЕсли;

			ИмяПараметра = ЭлементДокумента.Текст;
			
			// Возможно, что из-за форматирования текст параметра разбился на несколько html-элементов
			// и надо собрать их в один
			Пока Сч < МассивЭлементов.Количество() - 1
				И Прав(ИмяПараметра, 1) <> "}" Цикл
					СледующийЭлементДокумента = МассивЭлементов[Сч + 1];
					Если ТипЗнч(СледующийЭлементДокумента) <> Тип("ТекстФорматированногоДокумента") Тогда
						Прервать;
					КонецЕсли;
				
					ИмяПараметра = ИмяПараметра + СледующийЭлементДокумента.Текст;
					Сч = Сч + 1;
					ЭлементыКОбнулению.Добавить(СледующийЭлементДокумента);
			КонецЦикла;	
			Если Лев(ИмяПараметра, 1) = "{" Тогда
				ИмяПараметра = Сред(ИмяПараметра, 2);
			КонецЕсли;
			Если Прав(ИмяПараметра, 1) = "}" Тогда
				ИмяПараметра = Сред(ИмяПараметра, 1, СтрДлина(ИмяПараметра) - 1);
			КонецЕсли;
			
			Успешно = Истина;
			Если ЗначениеЗаполнено(ИмяПараметра) Тогда
				Значение = ЗначениеПараметра(ИмяПараметра, СведенияОбОбъекте, ЗначенияСоставногоПараметра, Успешно);
				Если СтрНайти(Значение, "%1%") > 0 Тогда
					// Это составной параметр, вставляем его значение по частям
					
					ДлинаДобавленногоТекста = ДлинаДобавленногоТекста 
						+ ВставитьСоставноеЗначение(Значение, ЗначенияСоставногоПараметра, ФорматированныйДокумент, ЭлементДокумента);
					ЗначенияСоставногоПараметра.Очистить();
					Значение = "";
				Иначе
					ДлинаДобавленногоТекста = ДлинаДобавленногоТекста +  СтрДлина(Значение);
				КонецЕсли;
				
				ЭлементДокумента.Текст = Значение;
			Иначе
				ЭлементДокумента.Текст = "";
			КонецЕсли;
	        Для Каждого Элемент Из ЭлементыКОбнулению Цикл
				Элемент.Текст = "";
			КонецЦикла;
			
			Если Успешно Тогда
				// Заменяем цвет фона по умолчанию
				Если ЭлементДокумента.ЦветФона = ЦветФонаЗаменяемогоПараметра Тогда
					ЭлементДокумента.ЦветФона = ЦветПоУмолчанию;
					ПоследнийТекстовыйЭлемент = ЭлементДокумента;
				КонецЕсли;
				
				Для Каждого Элемент Из ЭлементыКОбнулению Цикл
					Если Элемент.ЦветФона = ЦветФонаЗаменяемогоПараметра Тогда
						Элемент.ЦветФона = ЦветПоУмолчанию;
						ПоследнийТекстовыйЭлемент = Элемент;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Сч = Сч + 1;

		КонецЦикла;
		// При расположении подряд нескольких параметров между ними могут появляться 
		// пробелы или знаки препинания, закрашенные цветом ЦветФонаЗаменяемогоПараметра. 
		// Чтобы после замены всех соседних параметров эти пробелы не выделялсь, снимем у них фон.
		Если ПоследнийТекстовыйЭлемент <> Неопределено Тогда
			
			ЕстьДанные = ПоследнийТекстовыйЭлемент.ЗакладкаКонца <> ЗакладкаКонцаДокумента;
			
			Пока ЕстьДанные Цикл
				ПозицияПоследнегоТекстовогоЭлемента = ФорматированныйДокумент.ПолучитьПозициюПоЗакладке(
					ПоследнийТекстовыйЭлемент.ЗакладкаКонца);
				
				ПозицияКонцаДокумента = ФорматированныйДокумент.ПолучитьПозициюПоЗакладке(ЗакладкаКонцаДокумента);
				
				ПозицияПоследнегоТекстовогоЭлемента = Мин(ПозицияПоследнегоТекстовогоЭлемента + ДлинаДобавленногоТекста + 1, ПозицияКонцаДокумента);
				ЗакладкаПоПозиции = ФорматированныйДокумент.ПолучитьЗакладкуПоПозиции(ПозицияПоследнегоТекстовогоЭлемента);
				
				МассивЭлементов = ФорматированныйДокумент.ПолучитьЭлементы(ПоследнийТекстовыйЭлемент.ЗакладкаКонца, ЗакладкаПоПозиции);
				ПоследнийТекстовыйЭлемент = Неопределено;
				Для Каждого ЭлементДокумента Из МассивЭлементов Цикл
					Если ТипЗнч(ЭлементДокумента) <> Тип("ТекстФорматированногоДокумента") Тогда
						ПоследнийТекстовыйЭлемент = Неопределено;
						Прервать;
					КонецЕсли;
					
					Если СтрНайти(ЭлементДокумента.Текст, "{") > 0 Тогда
						// Начало нового параметра, дальше не обрабатываем
						ПоследнийТекстовыйЭлемент = Неопределено;
						Прервать;
					КонецЕсли;
					
					Если ЭлементДокумента.ЦветФона = ЦветФонаЗаменяемогоПараметра Тогда
						ЭлементДокумента.ЦветФона = ЦветПоУмолчанию;
						ПоследнийТекстовыйЭлемент = ЭлементДокумента;
					КонецЕсли;
					
				КонецЦикла;
				
				Если ПоследнийТекстовыйЭлемент = Неопределено Тогда
					ЕстьДанные = Ложь;
				ИначеЕсли ФорматированныйДокумент.ПолучитьПозициюПоЗакладке(ПоследнийТекстовыйЭлемент.ЗакладкаКонца) <= ПозицияПоследнегоТекстовогоЭлемента Тогда
					ЕстьДанные = Ложь;
				Иначе
					ЕстьДанные = ПоследнийТекстовыйЭлемент.ЗакладкаКонца <> ЗакладкаКонцаДокумента;
				КонецЕсли;
				
			КонецЦикла;

		КонецЕсли;
		
		// Ищем следующий параметр
		ОбластьНачалоПараметра = ФорматированныйДокумент.НайтиТекст("{", ОбластьНачалоПараметра.ЗакладкаНачала);
		

		
	КонецЦикла;	
	
КонецПроцедуры	


#КонецОбласти 